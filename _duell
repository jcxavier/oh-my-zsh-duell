#compdef duell
#autoload

local -a _1st_arguments
_1st_arguments=(
    'create:Used for creating a default project in the current folder.'
    'build:Builds the specified platform. Needs to be run in a folder with a duell_project.xml.'
    'update:Checks all the dependencies of the current project in the current directory and updates them.'
    'setup:Sets up the environment that needs to be run once for a specified platform.'
    'self_setup:Basic setup for the duell tool. Run me first!'
    'repo_list:Shows the repository list. You can add and remove urls from it.'
)

local -a _inherited_options
_inherited_options=(
    '(-verbose)-verbose[Every possible logging information will be shown.]' \
    '(-mute)-mute[No output will be printed at all.]' \
    '(-no-color)-no-color[Color markup will be stripped out of the output.]' \
    '(-yestoall)-yestoall[Answers "y" to all questions and default to all questions.]' \
    '(-help)-help[Show help banner of specified command.]'
)

local -a _build_options
_build_options=(
    '(-fast)-fast[Use this command if you are just iterating haxe code.]' \
    '(-publish)-publish[Outputs a releasable package into the configured "Publish" folder.]' \
    '(-norun)-norun[The application won'\''t run, just runs the prebuild and build steps.]' \
    '(-test)-test[Launches a server listening to a configured port and then runs the application.]' \
    '(-clean)-clean[Clean up the Export folder and the native extensions. Nothing else is done except cleaning up.]' \
    '(-noprebuild)-noprebuild[Does not parse the configuration, meaning the changes to the .xml files are not processed.]' \
    '(-nobuild)-nobuild[Does not build. Haxe code does not get recompiled, and target platform app packages are not remade.]'
)

local -a _build_android_options
_build_android_options=(
    '(-debug)-debug[Use this argument if you want to build in debug.]'
)

local -a _build_ios_options
_build_ios_options=(
    '(-debug)-debug[Use this argument if you want to build in debug.]' \
    '(-simulator)-simulator[Use this if you want to application to be built and run on the simulator.]' \
    '(-armv7)-armv7[Use this if you want to build for armv7.]' \
    '(-armv7s)-armv7s[Use this if you want to build for armv7s.]' \
    '(-arm64)-arm64[Use this if you want to build for arm64.]'
)

local -a _build_flash_options
_build_flash_options=(
    '(-debug)-debug[Use this argument if you want to build in debug.]' \
    '(-browser)-browser[Use this argument to make the app run on browser. The default is slimerjs.]' \
    '(-advanced-telemetry)-advanced-telemetry[Use this argument to enable advanced-telemetry for profiling in scout.]' \
    '(-slimerjs)-slimerjs[Use this argument to make the app run on slimerjs which is a standalone tiny firefox.]'
)

local -a _build_html5_options
_build_html5_options=(
    '(-debug)-debug[Use this argument if you want to build in debug.]' \
    '(-browser)-browser[Use this argument to make the app run on browser. The default is slimerjs.]' \
    '(-slimerjs)-slimerjs[Use this argument to make the app run on slimerjs which is a standalone tiny firefox.]'
)

local -a _update_options
_update_options=(
    '(-overridebranch)-overridebranch[If existent, sets the versions of each library to the specified branch.]'
)

local -a _setup_options
_setup_options=(
    '(-v)-v[Specifies the required setup version.]'
)

local -a _repo_list_options
_repo_list_options=(
    '(-add)-add[Adds a new url to the end of the repository list.]' \
    '(-removeAll)-removeAll[ARemoves all urls from the repository list. Resets to the default url.]' \
    '(-addFront)-addFront[Adds a new url to front of the repository list.]' \
    '(-reverse)-reverse[Reverses the order of all urls in the repository list.]' \
    '(-remove)-remove[Removes a url from the repository list.]'
)

__first_command_list ()
{
    local expl
    declare -a tasks

    tasks=(create build update setup self_setup repo_list)

    _wanted tasks expl 'help' compadd $tasks
}

_build_plugin_list() {
    _wanted application expl 'build' compadd $(command grep -hor "\"duellbuild.*\"" ~/.duell/lib_list | sort -u | awk '{ $0 = substr($0, 12); print substr($0, 0, length($0) -1); }')
}

_create_plugin_list() {
    _wanted application expl 'create' compadd $(command grep -hor "\"duellcreate.*\"" ~/.duell/lib_list | sort -u | awk '{ $0 = substr($0, 13); print substr($0, 0, length($0) -1); }')
}

_setup_plugin_list() {
    _wanted application expl 'setup' compadd $(command grep -hor "\"duellsetup.*\"" ~/.duell/lib_list | sort -u | awk '{ $0 = substr($0, 12); print substr($0, 0, length($0) -1); }')
}

_duell_build()
{
    local curcontext="$curcontext" state line
    typeset -A opt_args

    _arguments -C \
        ':command:->command' \
        '*::options:->options'

   case $state in
        (command)
            _describe -t commands "duell build"
            return
        ;;

        (options)
            case $line[1] in
                (android)
                    _arguments \
                        $_inherited_options \
                        $_build_options \
                        $_build_android_options
                ;;

                (ios)
                    _arguments \
                        $_inherited_options \
                        $_build_options \
                        $_build_ios_options
                ;;

                (flash)
                    _arguments \
                        $_inherited_options \
                        $_build_options \
                        $_build_flash_options
                ;;

                (html5)
                    _arguments \
                        $_inherited_options \
                        $_build_options \
                        $_build_html5_options
                ;;

                *)
                    _arguments \
                        $_inherited_options \
                        $_build_options
                ;;
            esac
            return
        ;;
    esac
}

local curcontext="$curcontext" state line
typeset -A opt_args

_arguments -C \
    $_inherited_options \
    ':command:->command' \
    '*::options:->options'

case $state in
  (command)
      _describe -t commands "duell" _1st_arguments
      return
  ;;

  (options)
    case $line[1] in
        (create)
            _arguments \
                $_inherited_options \
                ':feature:_create_plugin_list'
        ;;

        (build)
            _duell_build
            _arguments \
                ':feature:_build_plugin_list'
        ;;

        (update)
            _arguments \
                $_inherited_options \
                $_update_options
        ;;

        (setup)
            _arguments \
                $_inherited_options \
                $_setup_options \
                ':feature:_setup_plugin_list'
        ;;

        (self_setup)
            _arguments \
                $_inherited_options
        ;;

        (repo_list)
            _arguments \
                $_inherited_options \
                $_repo_list_options
        ;;
    esac
  ;;
esac
